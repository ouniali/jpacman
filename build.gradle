plugins {
    id 'application'
    id 'java'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '6.4.8'
}

sourceSets {
    defaultTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/default-test/java')
        }
        resources.srcDir file('src/default-test/resources')
    }
}

configurations {
    defaultTestImplementation.extendsFrom testImplementation
    defaultTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.guava:guava:$guavaVersion"

    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugsAnnotationsVersion"

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.4.1"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
}

task defaultTest(type: Test) {
    testClassesDirs = sourceSets.defaultTest.output.classesDirs
    classpath = sourceSets.defaultTest.runtimeClasspath
}
test.dependsOn defaultTest

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

defaultTest {
    useJUnitPlatform()
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestReport {
    executionData tasks.withType(Test)
    reports {
        csv.required = true
        xml.required = true
    }
}

checkstyle {
    configFile file("checkstyle.xml")
    ignoreFailures = true
}

pmd {
    ruleSetFiles "pmd-rules.xml"
    ruleSets = []
    ignoreFailures = true
}

spotbugs {
    ignoreFailures = true
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
    reports {
        create("html") {
            required = true
        }
        create("xml") {
            required = false
        }
    }
}

task staticAnalysis(type: GradleBuild) {
    tasks = [
            'clean',
            'checkstyleMain',
            'checkstyleTest',
            'checkstyleDefaultTest',
            'pmdMain',
            'pmdTest',
            'pmdDefaultTest',
            'spotbugsMain',
            'spotbugsTest',
            'spotbugsDefaultTest',
    ]
}

application {
    mainClass = 'nl.tudelft.jpacman.Launcher'
}